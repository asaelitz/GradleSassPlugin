buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:1.2.1"
    }
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'java-gradle-plugin'
apply plugin: "com.gradle.plugin-publish"

apply from: "${projectDir}/../gradle/publishing.gradle"

group 'org.asaelitz.gradle.sass'
version '1.2.4'

sourceCompatibility = 1.8

ext {
    publishingEnabled = project.hasProperty("enablePublishing")
    sonatypeUsername = project.hasProperty('sonatypeUsername') ? project.getProperty('sonatypeUsername') : ''
}


repositories {
    mavenCentral()
}

dependencies {
    compileOnly gradleApi()
    compileOnly 'com.vaadin:vaadin-sass-compiler:0.9.13'

    testImplementation group: 'junit', name: 'junit', version: '4.11'
    testImplementation group: 'commons-io', name: 'commons-io', version: '2.4'
    testImplementation gradleTestKit()
}

task sourceJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

//pluginBundle {
//    website.set("https://github.com/asaelitz/GradleSassPlugin")
//    vcsUrl.set("https://github.com/asaelitz/GradleSassPlugin")
//    description = 'Sass Plugin with support for multiple build configurations within project'
//    tags = ['sass', 'scss', 'asaelitz']
//    plugins {
//        create("sass") {
//            id = 'org.asaelitz.gradle.sass'
//            displayName = project.getProperty('artifactName')
//            description = project.getProperty('artifactDescription')
//            implementationClass = 'org.asaelitz.gradle.sass.SassPlugin'
//        }
//    }
//}

publishing {
    repositories {
        maven {
            name "sonatypeSnapshots"
            url 'https://oss.sonatype.org/content/repositories/snapshots'
            credentials {
                username project.sonatypeUsername
                password "-- not specified --"
            }
        }
        maven {
            name "sonatypeStaging"
            url 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            credentials {
                username project.sonatypeUsername
                password "-- not specified --"
            }
        }
    }
}

if (publishingEnabled) {
    afterEvaluate {
        publishing.publications {
            pluginMaven {
                artifact sourceJar
                artifact javadocJar

                project.ext.fillPom(pom, project.getProperty('artifactName'), project.getProperty('artifactDescription'))
            }

            sassPluginMarkerMaven {
                project.ext.fillPom(pom, project.getProperty('pluginmarker.artifactName'), project.getProperty('pluginmarker.artifactDescription'))
            }
        }

        signing {
            publishing.publications.all { publication ->
                sign publication
                required { !gradle.taskGraph.allTasks.findAll { it.name ==~ /.*Sonatype.*/ }.isEmpty() }
            }
        }
    }
}
